plugins {
  id 'java'
  id 'gradle-clojure.clojure' version '0.1.0'
  id 'maven-publish'
  id 'org.ajoberstar.stutter' version '0.2.0'
  id 'java-gradle-plugin'
  id 'com.gradle.plugin-publish' version '0.9.7'
}

configurations {
  bundle
}

dependencies {
  // gradle
  compile gradleApi()
  compileOnly 'org.clojure:clojure:1.8.0'
  compile project(':modules:gradle-clojure-tools')

  // compat testing
  compatTestCompile gradleTestKit()
  compatTestCompile 'org.clojure:clojure:1.8.0'
  compatTestCompile 'org.clojure:tools.namespace:0.2.11'
  compatTestCompile 'junit:junit:4.12'
  compatTestCompile 'org.ajoberstar:ike.cljj:0.4.0'
}

stutter {
  supports '4.1'
}

plugins.withId('eclipse') {
  eclipse.classpath.plusConfigurations += [configurations.compatTestCompile]
}

// BEGIN Hacky way to run clojure.test through JUnit4
compileCompatTestClojure {
  aotCompile = true
}

tasks.withType(Test).matching { it.name.startsWith('compatTest') }.all {
  inputs.dir 'src/compatTest/projects'
  systemProperty 'clojure.test.dirs', 'src/compatTest/clojure'
  systemProperty 'stutter.projects', 'src/compatTest/projects'
}
// END Hacky way to run clojure.test through JUnit4

publishing {
  publications {
    main(MavenPublication) {
      from components.java
    }
  }
}

gradlePlugin {
  plugins {
    clojureBasePlugin {
      id = 'gradle-clojure.clojure-base'
      implementationClass = 'gradle_clojure.plugin.ClojureBasePlugin'
    }
    clojurePlugin {
      id = 'gradle-clojure.clojure'
      implementationClass = 'gradle_clojure.plugin.ClojurePlugin'
    }
  }
}

pluginBundle {
  website = 'https://github.com/gradle-clojure/gradle-clojure'
  vcsUrl = 'https://github.com/gradle-clojure/gradle-clojure.git'
  description = 'Clojure and Clojurescript language support for Gradle'
  plugins {
    clojureBasePlugin {
      id = 'gradle-clojure.clojure-base'
      displayName = 'Clojure base language plugin for Gradle'
      tags = ['clojure', 'language']
    }
    clojurePlugin {
      id = 'gradle-clojure.clojure'
      displayName = 'Clojure language plugin for Gradle'
      tags = ['clojure', 'language']
    }
  }
}
