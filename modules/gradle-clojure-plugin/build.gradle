plugins {
  id 'java'
  id 'gradle-clojure.clojure'
  id 'maven-publish'
  id 'org.ajoberstar.stutter'
  id 'java-gradle-plugin'
  id 'com.gradle.plugin-publish'
}

dependencies {
  // gradle
  compile gradleApi()

  // clojure runtime
  compileOnly 'org.clojure:clojure:1.8.0'
  compile 'us.bpsm:edn-java:0.5.0'

  // compat testing
  compatTestCompile gradleTestKit()
  compatTestCompile 'org.clojure:clojure:1.8.0'
  compatTestCompile 'org.clojure:tools.namespace:0.3.0-alpha4'
  compatTestCompile 'nrepl:nrepl:0.3.1'
  compatTestCompile 'junit:junit:4.12'
  compatTestCompile 'org.ajoberstar:ike.cljj:0.4.1'
}

task writeProperties(type: WriteProperties) {
  outputFile = "${buildDir}/gradle-clojure.properties"
  property 'version', version
}

processResources {
  from writeProperties
}

stutter {
  includeActiveRc = true
  // Given how long the tests take, do something more "sparse" instead of all versions we support
  java(8) {
    compatible '4.1'
    compatibleRange '4.8-rc-1'
  }
  java(9) {
    compatible '4.2.1'
    compatibleRange '4.8-rc-1'
  }
}

plugins.withId('eclipse') {
  eclipse.classpath.plusConfigurations += [configurations.compatTestCompile]
}

compileCompatTestClojure {
  options.aotCompile = true
  namespaces.add 'gradle-clojure.tools.logger'
  namespaces.add 'gradle-clojure.tools.clojure-test-junit4'
}

tasks.withType(Test).matching { it.name.startsWith('compatTest') }.all {
  dependsOn ':modules:gradle-clojure-tools:publishToMavenLocal'

  inputs.dir 'src/compatTest/projects'
  systemProperty 'clojure.test.dirs', 'src/compatTest/clojure'
  systemProperty 'stutter.projects', 'src/compatTest/projects'
  systemProperty 'org.gradle.testkit.dir', file('build/stutter-test-kit').absolutePath
}

publishing {
  publications {
    pluginMaven(MavenPublication) {
      artifact sourcesJar
    }
  }
}

gradlePlugin {
  plugins {
    clojureBase {
      id = 'gradle-clojure.clojure-base'
      implementationClass = 'gradle_clojure.plugin.clojure.ClojureBasePlugin'
    }
    clojure {
      id = 'gradle-clojure.clojure'
      implementationClass = 'gradle_clojure.plugin.clojure.ClojurePlugin'
    }
    clojurescriptBase {
      id = 'gradle-clojure.clojurescript-base'
      implementationClass = 'gradle_clojure.plugin.clojurescript.ClojureScriptBasePlugin'
    }
    // clojurescript {
    //   id = 'gradle-clojure.clojurescript'
    //   implementationClass = 'gradle_clojure.plugin.clojurescript.ClojureScriptPlugin'
    // }
  }
}

pluginBundle {
  website = 'https://github.com/gradle-clojure/gradle-clojure'
  vcsUrl = 'https://github.com/gradle-clojure/gradle-clojure.git'
  description = 'Clojure and ClojureScript language support for Gradle'
  plugins {
    clojureBase {
      id = 'gradle-clojure.clojure-base'
      displayName = 'Clojure base language plugin for Gradle'
      tags = ['clojure', 'language']
    }
    clojure {
      id = 'gradle-clojure.clojure'
      displayName = 'Clojure language plugin for Gradle'
      tags = ['clojure', 'language']
    }
    clojurescriptBase {
      id = 'gradle-clojure.clojurescript-base'
      displayName = 'ClojureScript base language plugin for Gradle'
      tags = ['clojurescript', 'language']
    }
    // clojurescript {
    //   id = 'gradle-clojure.clojurescript'
    //   displayName = 'ClojureScript language plugin for Gradle'
    //   tags = ['clojurescript', 'language']
    // }
  }
  mavenCoordinates {
    groupId = project.group
    artifactId = project.name
    version = project.version
  }
}
