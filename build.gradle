plugins {
  id 'java'
  id 'maven-publish'
  id 'org.ajoberstar.grgit' version '2.0.0'
  id 'org.ajoberstar.reckon' version '0.2.1'
  id 'com.diffplug.gradle.spotless' version '3.4.1'
  id 'java-gradle-plugin'
  id 'com.gradle.plugin-publish' version '0.9.7'
}

group = 'gradle-clojure'

reckon {
  normal = scopeFromProp()
  preRelease = stageFromProp('milestone', 'rc')
}

repositories {
  jcenter()
}

dependencies {
  // gradle
  gradleApi()
}

gradlePlugin {
  plugins {
    clojurePlugin {
      id = 'gradle-clojure.clojure'
      implementationClass = 'gradle_clojure.ClojurePlugin'
    }
  }
}

pluginBundle {
  website = 'https://github.com/gradle-clojure/gradle-clojure'
  vcsUrl = 'https://github.com/gradle-clojure/gradle-clojure.git'
  description = 'Clojure and Clojurescript language support for Gradle'
  plugins {
    clojurePlugin {
      id = 'gradle-clojure.clojure'
      displayName = 'Clojure language plugin for Gradle'
      tags = ['clojure', 'language']
    }
  }
}

// ensure consistent formatting
spotless {
  java {
    googleJavaFormat()
    licenseHeaderFile file('gradle/HEADER')
  }
  format('gradle') {
    target '**/*.gradle'
    trimTrailingWhitespace()
    indentWithSpaces(2)
    endWithNewline()
  }
}

wrapper {
  gradleVersion = '4.1'
}
