plugins {
  id 'org.ajoberstar.grgit' version '2.2.0'
  id 'org.ajoberstar.reckon' version '0.6.0'
  id 'com.diffplug.gradle.spotless' version '3.10.0'

  id 'gradle-clojure.clojure' version '0.3.1' apply false
  id 'org.ajoberstar.stutter' version '0.3.0' apply false
  id 'com.gradle.plugin-publish' version '0.9.10' apply false

  // TODO remove when Gradle dependency locking is available
  id 'nebula.dependency-lock' version '5.0.4' apply false
}

reckon {
  normal = scopeFromProp()
  preRelease = stageFromProp('beta', 'rc', 'final')
}

allprojects {
  apply plugin: 'com.diffplug.gradle.spotless'
  apply plugin: 'nebula.dependency-lock'

  group = 'io.github.gradle-clojure'

  repositories {
    jcenter()
    maven { url = 'https://clojars.org/repo' }
  }

  plugins.withId('maven-publish') {
    publishing {
      repositories {
        maven {
          name = 'bintray'
          url = 'https://api.bintray.com/maven/gradle-clojure/maven/gradle-clojure/;publish=1'
          credentials {
            username = System.env['BINTRAY_USER']
            password = System.env['BINTRAY_KEY']
          }
        }
        maven {
          name = 'clojars'
          url = 'https://clojars.org/repo'
          credentials {
            username = System.env['CLOJARS_USER']
            password = System.env['CLOJARS_PASSWORD']
          }
        }
      }
    }
  }

  plugins.withId('java') {
    sourceCompatibility = '1.8'

    task sourcesJar(type: Jar) {
      from sourceSets.main.allSource
      classifier = 'sources'
    }

    // ensure consistent formatting
    spotless {
      java {
        importOrder 'java', 'javax', ''
        removeUnusedImports()
        eclipse().configFile rootProject.file('gradle/eclipse-java-formatter.xml')
      }
      format('gradle') {
        target '**/*.gradle'
        trimTrailingWhitespace()
        indentWithSpaces(2)
        endWithNewline()
      }
    }
  }
}
